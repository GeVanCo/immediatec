#!/usr/bin/perl -p
########################################################################
#
# $Id: gaIdke.smudge 1.11 $
# $IdBlockFurtherExpansion$
#
# Git filters to implement RCS $Id: keyword expansion
#
# smudge: insert git SHA-1 hash, iso commit date and author name before the
#         final $ of a clean $Id string, which is '$Id: <filename> <ver> $'.
#         <filename> and <ver> can be any non white-space characters except
#         <filename> and <ver> may not start with a $ to prevent a Heisenbug.
#
#         Only 'clean' $Id strings are smudged. This makes sure that any
#         $Id strings imported from legacy repositories are not modified.
#
# modified extensively from code by Kimmo R. M. Hovi, Fair Warning, Inc.
#
# Usage:      .git_filter/gaIdke.smudge file_path < file_contents
#
# NOTE:   gaIdke.smudge itself is not smudged on checkout because gaIdke.smudge
#         is temporarily removed during gaIdke-post-commit before git checkout.
#
########################################################################

BEGIN {
    $path = shift;
    if ($path =~ m/.*\/(.*)/) {
	$filename = $1;
    } else {
	$filename = $path;
    }
    $log = qx(git log -1 --pretty=format:"%h %ai %an" $path);
    $allowIdExpansion = 1;			# allow $Id expansion initially
}

if (m/\$Id/ and $allowIdExpansion) {		# speed up scan by pre-selecting $Id lines
    if (m/\$IdBlockFurtherExpansion\$/) {
	$allowIdExpansion = 0;			# block $Id expansion for texts describing this feature
    } else {
	s/\$Id:\s+([^\$\s]\S*)\s+([^\$\s]\S*)\s+\$/\$Id: $filename $2 $log \$/;
    }
}
